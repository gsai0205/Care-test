apply plugin: "groovy"
apply plugin: "idea"

repositories {
    mavenCentral()
}

// The drivers we want to use
ext.drivers = ["firefox", "chrome"]

configurations {
    testCompile.transitive = true
}


dependencies {



    def gebVersion = "0.9.2"
    def seleniumVersion = "2.40.0"

    // If using Spock, need to depend on geb-spock
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"

    // If using JUnit, need to depend on geb-junit (3 or 4)
    testCompile "org.gebish:geb-junit4:$gebVersion"

    testCompile "org.codehaus.groovy:groovy:2.2.2"

    testCompile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"

    // Drivers
    drivers.each { driver ->
        testCompile "org.seleniumhq.selenium:selenium-$driver-driver:$seleniumVersion"
        testRuntime "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    }
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        reports {
            html.destination = reporting.file("$name/tests")
            junitXml.destination = file("$buildDir/test-results/$name")
        }

        systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
        systemProperty "geb.env", driver

        // If you wanted to set the baseUrl in your buildâ€¦
        // systemProperty "geb.build.baseUrl", "http://myapp.com"
    }
}


test {
    dependsOn drivers.collect { tasks["${it}Test"] }
    enabled = false
}

